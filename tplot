#!/usr/bin/perl -w

use Term::ANSIColor; 
use Getopt::Std;
use Pod::Usage;
use List::Util qw[min max];
use Scalar::Util qw(looks_like_number);

$0 =~/.*\/(.*)/;

my %options= ();
getopts("b:k:f:s:y:l:hpev:",\%options);


########################################################
#  Preparation                                         #
########################################################

# Exit if -h is typed or -k is not used
if (defined $options{"h"} || not( defined $options{k})) {

   pod2usage(-verbose=>2, -noperldoc=>1);
}


# Get row numbers
my @col_nums   = split(",", $options{k});
my $num_series = scalar @col_nums;


# Record legends;
my @legends = ();

if (defined $options{l}) {

    for ( split(",", $options{l}) ){

        push @legends, $_;
    }
}

# Screen matrix size 
my $width = 100; 
my $height = 13;


# Handling size option -s 
if (defined $options{s}) {

    my @items = split(",", $options{s});

    if (@items == 1) { 
        $height = $items[0];

    } elsif ( @items == 2) {
        if ($items[0] ne "") {
            $height = $items[0];
            $width = $items[1]; 

        } else {
            $width = $items[1];
        }
    }
}


########################################################
#   Read Data                                          #
########################################################
my @datamatrix = ();
my @labels = ();
my @headers = ();

my $global_max = -1e32; 
my $global_min = 1e32;
my $global_abs_max;

# vertical line column index
my $vl_index = 9999;

my $row = 0;

for (<>) {
    chomp $_;

    my @items = split(" ", $_);

    # Determine if first row are headers 
    if ($row == 0) {
        my $num_non_numeric;

        $num_non_numeric+=not(looks_like_number($_)) for @items[map {$_-1} @col_nums];

        if ($num_non_numeric>0) { $options{e} = "";};
    }

    if ($row ==0 && defined $options{e}) {
        @headers = map { $items[$_-1] } @col_nums;
        $row++;
        next;
    }

    # Read Labelss
    if (defined $options{b}) {
        push @labels, $items[$options{b}-1];
    }

    for (my $i=0;$i<$num_series; $i++ ) {

        my $value = $items[ $col_nums[$i] - 1 ];

        push @{$datamatrix[$i]}, $value;

        if ($value > $global_max) { $global_max = $value }; 
        if ($value < $global_min) { $global_min = $value };
    }
}


my $num_obs = scalar @{$datamatrix[0]};


# Handling y-axis option -y to determin final min,max 
if (defined $options{y}) {

   my @items = split(",", $options{y});

   if (@items == 1) { 
       $global_min = $items[0];

   } elsif (@items == 2) { 
       if ($items[0] ne "") { 
           $global_min = $items[0];
           $global_max = $items[1]; 

       } else {
           $global_max = $items[1];
       }
   }
}

$global_abs_max = max(abs($global_min), abs($global_max));



# Determine y axis format 
my $indent = " "x9;

my $ytick_label_max;
my $ytick_label_min;

if (defined $options{f}) {
    $ytick_label_max = formatLabel($options{f}, $global_max); 
    $ytick_label_min = formatLabel($options{f}, $global_min);

} else {
    my $format = "";

    if ($global_abs_max <= 1) {
        $format = "p";

    } elsif ($global_abs_max <= 1e5 ) {
        $format = "d";

    } elsif ($global_abs_max <= 1e6 ) {
        $format = "k";

    } elsif ($global_abs_max <= 1e9 ) {
        $format = "m";
    }

    $ytick_label_max = formatLabel($format, $global_max); 
    $ytick_label_min = formatLabel($format, $global_min);
}


# Find vertical line index
if (defined $options{v}) {
    for (my $row=0; $row<@labels; $row++) {
        if ($labels[$row] eq $options{v}) {
            $vl_index = getGridIndex($row, 0, $num_obs-1, $width);
        }
    }
}

########################################################
#   Fill up screen matrix                              #
########################################################
my @screen   = ();
my @crossing = ();
my @point_type = ();


# loop through series
for (my $s=0; $s < $num_series; $s++) {

    my $last_r_index;
    my $last_c_index;

    # loop through observations
    for (my $i=0; $i<$num_obs; $i++) {

        # Fill the point
        my $r_index = getGridIndex($datamatrix[$s][$i], $global_min, $global_max, $height);
        my $c_index = getGridIndex($i, 0, $num_obs-1, $width);

        if ($r_index < $height && $r_index >= 0)  {
            if (defined $screen[$r_index][$c_index]) {

                if ($s > $screen[$r_index][$c_index]) {

                    $screen[$r_index][$c_index]   = $s;
                    $crossing[$r_index][$c_index] = $s;
                }

            } else {
                $screen[$r_index][$c_index] = $s;
            }

            $point_type[$r_index][$c_index] = 1;
        }


        #Fill between points
        if ($i >0 && not(defined $options{p}) && defined $last_c_index && defined $last_r_index ){

            my $c_w = $last_c_index - $c_index;
            my $r_w = $last_r_index - $r_index;

            for (my $mv_c_indx = $last_c_index+1; $mv_c_indx < $c_index; $mv_c_indx++) {

                my $new_r_indx = int($last_r_index + ($mv_c_indx - $last_c_index)/ $c_w * $r_w + 0.5);

                if ($new_r_indx < $height && $new_r_indx >= 0) {

                    if (defined $screen[$new_r_indx][$mv_c_indx]) {

                        if ($s > $screen[$new_r_indx][$mv_c_indx]) {

                            $screen[$new_r_indx][$mv_c_indx] = $s;
                        }

                    } else {
                        $screen[$new_r_indx][$mv_c_indx] = $s;
                    }

                    $point_type[$new_r_indx][$mv_c_indx] = 0;
                }
            }
        }


        # Record last point indeces
        $last_r_index = $r_index;
        $last_c_index = $c_index;
    }
}

# Zero line
my $y_zero_index = getGridIndex(0, $global_min, $global_max, $height);




########################################################
#   Plot (print)                                       #
########################################################
print "\n";

my @colors = ('bright_blue', 'bright_red', 'yellow', 'bright_cyan', 'bright_green');



# Print main plot
for (my $r = $height-1; $r>=0; $r--) {

    # Ytick mark label 
    print color "white";

    if ( $r == 0 ) {
        print $ytick_label_min." ";

    } elsif ( $r == $height - 1) {
        print $ytick_label_max." ";

    } elsif ( $r == $y_zero_index) {
        print " "x8 . "0 ";
    }
    else {
        print $indent." ";
    }


    # print the row
    for (my $c=0; $c < $width; $c++) {

        if (defined $screen[$r][$c]) {

            my $dot_series_num = $screen[$r][$c];

            if ( defined $crossing[$r][$c] ) {
                print color $colors[$dot_series_num % @colors -1];
                print "x";

            } else {
                print color $colors[$dot_series_num % @colors -1];
                if ($point_type[$r][$c] == 1) {
                    print "o";
                } else {
                    print "-";
                }
            }

        } elsif ( $r == $y_zero_index and $c == 0 ) {
            print color "white";
            print "+";
        } elsif ( $r == $y_zero_index and $c%2 == 0) {
            print color "bright_black";
            print "-";
        } elsif ( $c == 0 ) {
            print color "white";
            print "|";
        } elsif ( $c == $vl_index ) {
            print color "bright_black";
            print "|";
        } else {
            print " ";
        }
    }

    # next row
    print "\n";
}

print color "white";


# ---------------------------
# Print x-axis and tick marks
# ---------------------------

#initialize xaxis
my $xaxis = "-" x $width;

#initialzie xaxis tickmark labels
my @xtick_label = ();
$xtick_label[0] = " " x $width;
$xtick_label[1] = " " x $width;

my $xlabel_layer = 0;  #xtick lable layer indicator

my $step = max(1, int($num_obs/10) ); #number of x labels

#Loop through each xaxis label
for ($i=0; $i<$num_obs; $i=$i+$step) {

    # get index
    my $index = getGridIndex($i, 0, $num_obs-1, $width);

    # mark tick mark
    substr($xaxis, $index , 1) = "|";

    # get label
    my $lab;

    if (defined $options{b}) {
        $lab = $labels[$i];
    } else {
        $lab = sprintf("%-4d", $i);
    }

    # mark label
    if ( substr($xtick_label[$xlabel_layer], $index-1, 1) ne " ") {
        $xlabel_layer = 1 - $xlabel_layer;
    }

    substr($xtick_label[$xlabel_layer], $index , length($lab)) = $lab;
}

# print xaxis and labels
print $indent." ". $xaxis."\n";
print $indent." ". $xtick_label[0]."\n";
print $indent." ". $xtick_label[1]."\n";


# Print Legends
print "\n";
print $indent." [ ";

for ( 1 .. $num_series) {

    print color $colors[$_ % @colors - 1];

    if (defined $legends[$_-1]) {
        print "o - $legends[$_-1] ";

    } elsif ( defined $options{e}) {
        print "o - $headers[$_-1] ";

    } else {
        print "o - $_ ";
    }
}
print color "white";
print "]\n";


########################################################
#   SUBROUTINES                                        #
########################################################
 
sub formatLabel {
    my ($format, $value) = @_;

    if ( $format eq "m" ) {
        $value = $value / 1e6; 
        return(sprintf("%8.1fM", $value));

    } elsif ( $format eq "k" ) {
        $value = $value / 1000; 
        return(sprintf("%8.1fk", $value));

    } elsif ( $format eq "p" ) {
        $value = $value * 100; 
        return(sprintf("%8.2f", $value)."%");

    } elsif ( $format eq "d" ) {
        $value = int($value);
        return(sprintf("%9s", commify($value)));

    } else {
        return(sprintf("%9.2e", $value));
    }
}


sub getGridIndex {

    my ($value, $min, $max, $num_grid) = @_;

    my $half_buck_size  = ($max - $min)/ ($num_grid - 1) / 2;

    my $index = int(  ($value - $min + $half_buck_size ) / ($max - $min) * ($num_grid-1) ); 

    return( $index );
}


sub commify {
    my $text = reverse $_[0];
    $text =~ s/(\d\d\d)(?=\d)(?!\d*\.)/$1,/g;
    return scalar reverse $text;
}



############################
### Usage Page         #####
############################

__END__

=head1 NAME

tplot - A simple terminal plotting tool

=head1 SYNOPSIS

tplot -k col1[,col2,..] [-b col] [-p] [-s hieght,width] [-y min,max] [-f format] [-l legend1, legend2, ..] [-h] [file]


=head1 OPTIONS

The following options are supported:

=over 12

=item B<-k> col1[,col2,..]

List the column numbers that are to be included in the plot.

=item B<-b> col

Column to be used for labels.

=item B<-p>

Plot points only, no connecting lines.

=item B<-s> height,width

The size of the plotting area. Default height is 15 and default width is 100.

=item B<-y> min,max

Set min and max of the y axis.

=item B<-f> format

Format of the y-axis label. If not selected, it will be decided based on the max absolute value of the input. "m" for millions. "p" for percentage. "k" for thousands.

=item B<-l> legend1, legend2,...

Legends for each series. If not selected, numbers will be used.

=item B<-h>

Print this help message.


=back

=cut

